pkgname=pulse-midi
pkgver=0.0.1
pkgrel=1
pkgdesc="USB Midi Controller for Pulse Audio"
arch=('any')
url="https://github.com/johnshaughnessy/pulse-midi"
license=('MIT')
depends=('nodejs')
source=("git+https://github.com/johnshaughnessy/pulse-midi")
sha256sums=('SKIP')
install=pulse-midi.install
package() {
  # Build in $srcdir
  cd "$srcdir/$pkgname"
  npm ci
  npm run build

  # Copy the built files from $srcdir to $pkgdir
  install -d "$pkgdir/usr/lib/$pkgname/dist"
  cp -r "$srcdir/$pkgname/dist" "$pkgdir/usr/lib/$pkgname/"

  # Install production dependencies to $pkgdir
  install -Dm644 "$srcdir/pulse-midi/package-lock.json" "$pkgdir/usr/lib/$pkgname/package-lock.json"
  install -Dm644 "$srcdir/pulse-midi/package.json" "$pkgdir/usr/lib/$pkgname/package.json"
  npm ci --production --omit=dev --prefix "$pkgdir/usr/lib/$pkgname"

  # Install the executable in $pkgdir
  install -Dm755 -T "$srcdir/$pkgname/pacman/pulse-midi.sh" "$pkgdir/usr/bin/pulse-midi"

  # Install the systemd user unit. (Requires enable and start)
  install -Dm644 "$srcdir/$pkgname/pacman/pulse-midi.service" "$pkgdir/usr/lib/systemd/user/pulse-midi.service"
}
